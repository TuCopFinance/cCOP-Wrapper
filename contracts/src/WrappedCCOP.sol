// SPDX-License-Identifier: MIT
// Compatible with OpenZeppelin Contracts ^5.0.0
pragma solidity ^0.8.20;

import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {IMailbox} from "@hyperlane-xyz/core/contracts/interfaces/IMailbox.sol";

contract WrappedCCOP is ERC20 {
    //ğŸ¬¥ğŸ¬±ğŸ¬¢ğŸ¬½ğŸ¬ºğŸ¬°ğŸ¬‘ğŸ¬°ğŸ¬šğŸ¬„ğŸ¬“ğŸ¬¨ğŸ¬˜ğŸ¬ªğŸ¬¹ğŸ¬¥ğŸ¬ğŸ¬±ğŸ¬ğŸ¬…ğŸ¬ŸğŸ¬ğŸ¬”ğŸ¬·ğŸ¬ğŸ¬¶ğŸ¬¸ğŸ¬”ğŸ¬´ğŸ¬¥ Events ğŸ¬ğŸ¬ğŸ¬¢ğŸ¬ºğŸ¬±ğŸ¬‹ğŸ¬¿ğŸ¬ªğŸ¬™ğŸ¬’ğŸ¬½ğŸ¬ƒğŸ¬»ğŸ¬¥ğŸ¬‘ğŸ¬¬ğŸ¬„ğŸ¬³ğŸ¬¥ğŸ¬ ğŸ¬·ğŸ¬•ğŸ¬†ğŸ¬³ğŸ¬œğŸ¬¼ğŸ¬“ğŸ¬·ğŸ¬¬ğŸ¬´
    event AdminChanged(address indexed previousAdmin, address indexed newAdmin);

    //ğŸ¬³ğŸ¬€ğŸ¬®ğŸ¬£ğŸ¬­ğŸ¬¦ğŸ¬ğŸ¬¤ğŸ¬¨ğŸ¬¯ğŸ¬²ğŸ¬°ğŸ¬’ğŸ¬“ğŸ¬©ğŸ¬…ğŸ¬“ğŸ¬®ğŸ¬¦ğŸ¬ ğŸ¬‡ğŸ¬¨ğŸ¬–ğŸ¬§ğŸ¬µğŸ¬¬ğŸ¬¨ğŸ¬ŒğŸ¬¥ğŸ¬¨ Errors ğŸ¬‚ğŸ¬¹ğŸ¬¾ğŸ¬ğŸ¬¬ğŸ¬«ğŸ¬¸ğŸ¬‰ğŸ¬¼ğŸ¬…ğŸ¬©ğŸ¬‚ğŸ¬¤ğŸ¬¶ğŸ¬¨ğŸ¬£ğŸ¬´ğŸ¬™ğŸ¬‘ğŸ¬½ğŸ¬§ğŸ¬·ğŸ¬­ğŸ¬ŠğŸ¬½ğŸ¬¢ğŸ¬œğŸ¬ğŸ¬¹ğŸ¬¹
    error mailboxNotAuthorized();
    error senderNotAuthorized();
    error chainIdNotAuthorized();
    error amountMustBeGreaterThanZero();
    error unwrappedTokenNotSet();
    error UnauthorizedAccount();
    error EmergencyStop();
    error WaitingPeriodNotExpired();

    //ğŸ¬˜ğŸ¬¾ğŸ¬ ğŸ¬“ğŸ¬ºğŸ¬ğŸ¬‹ğŸ¬²ğŸ¬‚ğŸ¬¯ğŸ¬šğŸ¬‰ğŸ¬¯ğŸ¬œğŸ¬ğŸ¬ƒğŸ¬¿ğŸ¬‹ğŸ¬…ğŸ¬²ğŸ¬½ğŸ¬¯ğŸ¬ŠğŸ¬ƒğŸ¬’ğŸ¬ğŸ¬®ğŸ¬°ğŸ¬ŒğŸ¬¥ Structs ğŸ¬»ğŸ¬—ğŸ¬ğŸ¬ğŸ¬ ğŸ¬·ğŸ¬¹ğŸ¬…ğŸ¬§ğŸ¬¡ğŸ¬¥ğŸ¬ğŸ¬ˆğŸ¬¨ğŸ¬‘ğŸ¬¢ğŸ¬¯ğŸ¬šğŸ¬ŠğŸ¬ğŸ¬¥ğŸ¬­ğŸ¬•ğŸ¬¡ğŸ¬¯ğŸ¬µğŸ¬¥ğŸ¬˜ğŸ¬ğŸ¬‰

    struct AddressTypeProposal {
        address current;
        address proposal;
        uint256 timeToAccept;
    }

    struct Bytes32Proposal {
        bytes32 current;
        bytes32 proposal;
        uint256 timeToAccept;
    }

    struct Uint32Proposal {
        uint32 current;
        uint32 proposal;
        uint256 timeToAccept;
    }

    //ğŸ¬¡ğŸ¬‚ğŸ¬±ğŸ¬¿ğŸ¬’ğŸ¬ğŸ¬³ğŸ¬³ğŸ¬³ğŸ¬ğŸ¬ğŸ¬ğŸ¬¯ğŸ¬¸ğŸ¬µğŸ¬ğŸ¬ˆğŸ¬ºğŸ¬ŒğŸ¬ŒğŸ¬ğŸ¬‹ğŸ¬¯ğŸ¬ˆğŸ¬ˆğŸ¬™ğŸ¬ğŸ¬ŸğŸ¬´ğŸ¬ State Variables ğŸ¬„ğŸ¬©ğŸ¬ğŸ¬•ğŸ¬šğŸ¬®ğŸ¬¤ğŸ¬´ğŸ¬–ğŸ¬µğŸ¬¶ğŸ¬”ğŸ¬„ğŸ¬©ğŸ¬ªğŸ¬¸ğŸ¬¿ğŸ¬†ğŸ¬½ğŸ¬ ğŸ¬³ğŸ¬°ğŸ¬ğŸ¬»ğŸ¬¼ğŸ¬€ğŸ¬³ğŸ¬ğŸ¬¹ğŸ¬

    uint256 private constant WAITING_PERIOD = 1 days;
    AddressTypeProposal private admin;
    Bytes32Proposal private cCOPAddress;
    Uint32Proposal private cCOPDomainId;
    AddressTypeProposal private mailboxAddress;
    bool private fuse = true;

    //ğŸ¬¦ğŸ¬§ğŸ¬·ğŸ¬‘ğŸ¬£ğŸ¬ºğŸ¬­ğŸ¬½ğŸ¬ğŸ¬©ğŸ¬±ğŸ¬›ğŸ¬©ğŸ¬ğŸ¬‡ğŸ¬ªğŸ¬•ğŸ¬²ğŸ¬‡ğŸ¬ˆğŸ¬¤ğŸ¬ğŸ¬—ğŸ¬±ğŸ¬°ğŸ¬ğŸ¬ŠğŸ¬ğŸ¬ğŸ¬† Modifier ğŸ¬·ğŸ¬“ğŸ¬¤ğŸ¬­ğŸ¬¹ğŸ¬¢ğŸ¬¶ğŸ¬‡ğŸ¬ŸğŸ¬²ğŸ¬˜ğŸ¬‘ğŸ¬–ğŸ¬¼ğŸ¬‚ğŸ¬§ğŸ¬ğŸ¬’ğŸ¬¨ğŸ¬‚ğŸ¬„ğŸ¬¼ğŸ¬ŒğŸ¬–ğŸ¬¤ğŸ¬ŸğŸ¬“ğŸ¬ğŸ¬»ğŸ¬¹

    modifier onlyAdmin() {
        if (msg.sender != admin.current) {
            revert UnauthorizedAccount();
        }
        _;
    }

    modifier checkFuse() {
        if (!fuse) {
            revert EmergencyStop();
        }
        _;
    }

    // ğŸ¬©ğŸ¬‚ğŸ¬†ğŸ¬ªğŸ¬–ğŸ¬€ğŸ¬¨ğŸ¬‡ğŸ¬³ğŸ¬‚ğŸ¬¶ğŸ¬¼ğŸ¬ªğŸ¬¥ğŸ¬ƒğŸ¬¤ğŸ¬ˆğŸ¬¥ğŸ¬¢ğŸ¬¿ğŸ¬‰ğŸ¬µğŸ¬„ğŸ¬‘ğŸ¬…ğŸ¬•ğŸ¬„ğŸ¬€ğŸ¬–ğŸ¬¬ Constructor ğŸ¬¤ğŸ¬¶ğŸ¬ğŸ¬£ğŸ¬‡ğŸ¬ğŸ¬´ğŸ¬·ğŸ¬»ğŸ¬µğŸ¬˜ğŸ¬§ğŸ¬‡ğŸ¬ˆğŸ¬”ğŸ¬ğŸ¬½ğŸ¬•ğŸ¬ğŸ¬¤ğŸ¬¡ğŸ¬²ğŸ¬–ğŸ¬¡ğŸ¬›ğŸ¬‚ğŸ¬ğŸ¬ğŸ¬§ğŸ¬”

    constructor(
        address _initialAdmin,
        address _mailbox,
        uint32 _domainId,
        address _address
    ) ERC20("Wrapped Celo Colombian Peso", "wcCOP") {
        admin.current = _initialAdmin;
        mailboxAddress.current = _mailbox;
        cCOPDomainId.current = _domainId;
        cCOPAddress.current = bytes32(uint256(uint160(_address)));
    }

    //ğŸ¬¨ğŸ¬ŸğŸ¬£ğŸ¬¡ğŸ¬‹ğŸ¬´ğŸ¬¹ğŸ¬‰ğŸ¬®ğŸ¬£ğŸ¬†ğŸ¬«ğŸ¬¨ğŸ¬ºğŸ¬ŠğŸ¬ ğŸ¬’ğŸ¬›ğŸ¬€ğŸ¬§ğŸ¬±ğŸ¬±ğŸ¬ğŸ¬˜ğŸ¬ƒğŸ¬‘ğŸ¬¶ğŸ¬¬ğŸ¬”ğŸ¬› Token Handling ğŸ¬œğŸ¬²ğŸ¬ğŸ¬œğŸ¬»ğŸ¬»ğŸ¬€ğŸ¬ƒğŸ¬ºğŸ¬ŠğŸ¬†ğŸ¬©ğŸ¬¡ğŸ¬ˆğŸ¬»ğŸ¬®ğŸ¬…ğŸ¬¬ğŸ¬°ğŸ¬ğŸ¬³ğŸ¬¥ğŸ¬±ğŸ¬¼ğŸ¬²ğŸ¬ğŸ¬ŸğŸ¬ºğŸ¬ºğŸ¬¥

    function handle(
        uint32 _origin,
        bytes32 _sender,
        bytes calldata _data
    ) external payable virtual {
        if (msg.sender != mailboxAddress.current) revert mailboxNotAuthorized();

        if (_sender != cCOPAddress.current) revert senderNotAuthorized();

        if (_origin != cCOPDomainId.current) revert chainIdNotAuthorized();

        (address to, uint256 amount) = abi.decode(_data, (address, uint256));

        _mint(to, amount);
    }

    function unwrap(
        address receiver,
        uint256 amount
    ) external payable checkFuse returns (bytes32 messageId) {
        if (amount == 0) revert amountMustBeGreaterThanZero();

        bytes memory payload = abi.encode(receiver, amount);

        uint256 quote = getQuote(receiver, amount);

        _burn(msg.sender, amount);

        messageId = IMailbox(mailboxAddress.current).dispatch{value: quote}(
            cCOPDomainId.current,
            cCOPAddress.current,
            payload
        );
    }

    function getQuote(
        address receiver,
        uint256 amount
    ) public view returns (uint256) {
        bytes memory payload = abi.encode(receiver, amount);
        return
            IMailbox(mailboxAddress.current).quoteDispatch(
                cCOPDomainId.current,
                cCOPAddress.current,
                payload
            );
    }

    //ğŸ¬†ğŸ¬›ğŸ¬†ğŸ¬©ğŸ¬ğŸ¬¢ğŸ¬‡ğŸ¬–ğŸ¬‰ğŸ¬¼ğŸ¬†ğŸ¬ŠğŸ¬¤ğŸ¬™ğŸ¬¼ğŸ¬©ğŸ¬£ğŸ¬¼ğŸ¬½ğŸ¬½ğŸ¬‘ğŸ¬ğŸ¬šğŸ¬¬ğŸ¬ƒğŸ¬ğŸ¬ğŸ¬€ğŸ¬¯ğŸ¬» Admin Functions ğŸ¬¿ğŸ¬«ğŸ¬‰ğŸ¬£ğŸ¬¡ğŸ¬ğŸ¬´ğŸ¬‰ğŸ¬»ğŸ¬©ğŸ¬›ğŸ¬¬ğŸ¬«ğŸ¬”ğŸ¬ˆğŸ¬ºğŸ¬ŠğŸ¬¤ğŸ¬”ğŸ¬²ğŸ¬¥ğŸ¬¬ğŸ¬ºğŸ¬¼ğŸ¬ ğŸ¬˜ğŸ¬µğŸ¬±ğŸ¬ğŸ¬¾

    function proposeNewAdminProposal(address _newAdmin) external onlyAdmin {
        admin.proposal = _newAdmin;
        admin.timeToAccept = block.timestamp + WAITING_PERIOD;
    }

    function cancelAdminProposal() external onlyAdmin {
        admin.proposal = address(0);
        admin.timeToAccept = 0;
    }

    function acceptAdminProposal() external {
        if (msg.sender != admin.proposal) {
            revert UnauthorizedAccount();
        }
        if (block.timestamp < admin.timeToAccept) {
            revert WaitingPeriodNotExpired();
        }

        address previousAdmin = admin.current;
        admin = AddressTypeProposal({
            current: admin.proposal,
            proposal: address(0),
            timeToAccept: 0
        });

        emit AdminChanged(previousAdmin, admin.current);
    }

    function proposeNewCCOPAddressProposal(
        bytes32 _newAddress
    ) external onlyAdmin {
        cCOPAddress.proposal = _newAddress;
        cCOPAddress.timeToAccept = block.timestamp + WAITING_PERIOD;
    }

    function cancelCCOPAddressProposal() external onlyAdmin {
        cCOPAddress.proposal = bytes32(0);
        cCOPAddress.timeToAccept = 0;
    }

    function acceptCCOPAddressProposal() external onlyAdmin {
        if (block.timestamp < cCOPAddress.timeToAccept) {
            revert WaitingPeriodNotExpired();
        }

        cCOPAddress = Bytes32Proposal({
            current: cCOPAddress.proposal,
            proposal: bytes32(0),
            timeToAccept: 0
        });
    }

    function proposeNewCCOPDomainIdProposal(
        uint32 _newDomainId
    ) external onlyAdmin {
        cCOPDomainId.proposal = _newDomainId;
        cCOPDomainId.timeToAccept = block.timestamp + WAITING_PERIOD;
    }

    function cancelCCOPDomainIdProposal() external onlyAdmin {
        cCOPDomainId.proposal = 0;
        cCOPDomainId.timeToAccept = 0;
    }

    function acceptCCOPDomainIdProposal() external onlyAdmin {
        if (block.timestamp < cCOPDomainId.timeToAccept) {
            revert WaitingPeriodNotExpired();
        }

        cCOPDomainId = Uint32Proposal({
            current: cCOPDomainId.proposal,
            proposal: 0,
            timeToAccept: 0
        });
    }

    function proposeNewMailboxAddressProposal(
        address _newMailbox
    ) external onlyAdmin {
        mailboxAddress.proposal = _newMailbox;
        mailboxAddress.timeToAccept = block.timestamp + WAITING_PERIOD;
    }

    function cancelMailboxAddressProposal() external onlyAdmin {
        mailboxAddress.proposal = address(0);
        mailboxAddress.timeToAccept = 0;
    }

    function acceptMailboxAddressProposal() external onlyAdmin {
        if (block.timestamp < mailboxAddress.timeToAccept) {
            revert WaitingPeriodNotExpired();
        }

        mailboxAddress = AddressTypeProposal({
            current: mailboxAddress.proposal,
            proposal: address(0),
            timeToAccept: 0
        });
    }

    function setFuse() external onlyAdmin {
        fuse = !fuse;
    }

    //ğŸ¬ºğŸ¬¸ğŸ¬ŒğŸ¬¬ğŸ¬¨ğŸ¬’ğŸ¬’ğŸ¬¥ğŸ¬ğŸ¬ğŸ¬ğŸ¬œğŸ¬»ğŸ¬§ğŸ¬¯ğŸ¬ğŸ¬ğŸ¬ƒğŸ¬ğŸ¬¿ğŸ¬¯ğŸ¬„ğŸ¬«ğŸ¬’ğŸ¬ğŸ¬ğŸ¬¬ğŸ¬ğŸ¬ğŸ¬’ Getters ğŸ¬‘ğŸ¬šğŸ¬¡ğŸ¬¿ğŸ¬™ğŸ¬•ğŸ¬ªğŸ¬œğŸ¬ğŸ¬’ğŸ¬½ğŸ¬£ğŸ¬‰ğŸ¬“ğŸ¬±ğŸ¬•ğŸ¬¤ğŸ¬’ğŸ¬ğŸ¬¨ğŸ¬»ğŸ¬»ğŸ¬‚ğŸ¬‰ğŸ¬‰ğŸ¬´ğŸ¬”ğŸ¬„ğŸ¬ğŸ¬™

    function getAdminStructure()
        external
        view
        returns (AddressTypeProposal memory)
    {
        return admin;
    }

    function getCCOPAddressStructure()
        external
        view
        returns (Bytes32Proposal memory)
    {
        return cCOPAddress;
    }

    function getCCOPDomainIdStructure()
        external
        view
        returns (Uint32Proposal memory)
    {
        return cCOPDomainId;
    }

    function getMailboxAddressStructure()
        external
        view
        returns (AddressTypeProposal memory)
    {
        return mailboxAddress;
    }

    function getFuse() external view returns (bool) {
        return fuse;
    }

    //ğŸ¬ğŸ¬˜ğŸ¬½ğŸ¬¹ğŸ¬šğŸ¬£ğŸ¬”ğŸ¬‚ğŸ¬†ğŸ¬ŒğŸ¬šğŸ¬ğŸ¬¬ğŸ¬¢ğŸ¬ŠğŸ¬¯ğŸ¬¨ğŸ¬·ğŸ¬ğŸ¬²ğŸ¬©ğŸ¬‚ğŸ¬ğŸ¬ªğŸ¬³ğŸ¬ ğŸ¬·ğŸ¬ºğŸ¬›ğŸ¬… ERC20 Overrides ğŸ¬ŒğŸ¬ŠğŸ¬¸ğŸ¬†ğŸ¬€ğŸ¬€ğŸ¬¥ğŸ¬ŠğŸ¬»ğŸ¬ğŸ¬¾ğŸ¬¿ğŸ¬¾ğŸ¬¸ğŸ¬ªğŸ¬¶ğŸ¬„ğŸ¬¸ğŸ¬¦ğŸ¬¹ğŸ¬ˆğŸ¬ŒğŸ¬˜ğŸ¬¼ğŸ¬¸ğŸ¬šğŸ¬•ğŸ¬£ğŸ¬ªğŸ¬™
    function decimals() public view override returns (uint8) {
        return 15;
    }
}
